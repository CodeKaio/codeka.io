---
date: "2025-01-17"
language: fr
tags:
- DevOps
- Terraform
- Vault
- Clever Cloud
title: Vault sur Clever Cloud
series: Terraform et Clever Cloud
draft: true
---

Pour les besoins des cours que je donne √† l'Universit√© de Lille, j'ai eu besoin de configurer un serveur Vault sur Clever Cloud.

Et [bien entendu]({{< ref "/books/iac-avec-terraform" >}}), j'ai fait tout √ßa avec Terraform.

Cet article d√©crit comment utiliser le provider Terraform de Clever Cloud pour d√©ployer un serveur Vault, le configurer pour l'authentification OIDC avec GitLab et y stocker quelques secrets √† titre d'exemple.

Le code de cet article est aussi disponible sur GitHub : https://github.com/juwit/terraform-clevercloud-playground/tree/main/vault.

> Cet article a √©t√© √©crit avec des commandes Terraform, mais fonctionne √©galement avec les commandes OpenTofu √©quivalentes.

## Architecture cible

Avant d'entrer dans la mise en pratique, il convient ici d'expliquer quelques choix illustr√©s par le sch√©ma suivant.

![](cc_vault.png)

Clever Cloud propose de d√©ployer des applications dans de [nombreux langages](https://www.clever-cloud.com/developers/doc/applications/). Pour h√©berger une instance Vault, le plus simple semblait d'utiliser une instance Docker.

Par d√©fault, Vault propose l'utilisation du backend de stockage _Integrated storage_ pour le stockage des donn√©es. √âtant donn√© la nature du d√©ploiement avec une instance Docker sur un seul n≈ìud et le fait que Clever Cloud ne supporte pas le stockage persistent pour ce type d'instance, il m'a sembl√© judicieux d'utiliser un backend de stockage externalis√©. Parmi les options propos√©es par Vault, 3 options sont envisageables sur Clever Cloud : les bases de donn√©es MySQL ou PostgreSQL, ou S3 _via_ l'impl√©mentation _Cellar_ fournie par Clever Cloud.

Le stockage externalis√© sur S3 ne supporte pas la haute disponibilit√© et pourrait s'av√©rer incompatible avec l'impl√©mentation _Cellar_ (cf. [les adaptations requises par le backend Terraform S3 pour Cellar]({{< ref "/posts/2024-12-31-terraform-clever-cloud#configuration-du-backend" >}})), donc je l'ai directement √©cart√© et j'ai privil√©gi√© l'impl√©mentation avec PostgreSQL.

L'authentification _via_ GitLab permet √† mes √©tudiants d'utiliser leur compte GitLab existant, en exploitant l'instance GitLab fournie par l'Universit√© de Lille. C'est donc tr√®s pratique pour eux (pas besoin d'avoir un compte ailleurs) et pour moi (pas besoin de provisionner des comptes).
J'aurais aussi pu utiliser une instance KeyCloak pour impl√©menter l'authentification, mais cela aurait complexifi√© inutilement l'impl√©mentation.

√Ä noter aussi que je ne suis pas expert Vault, donc je ne suis pas √† l'abri d'avoir fait une erreur de configuration quelque-part, alors attention si vous utilisez cette configuration en production :)

## SetUp de Terraform

Dans un article pr√©c√©dent, j'ai d√©j√† expliqu√© comment configurer [Terraform pour Clever Cloud]({{< ref "/posts/2024-12-31-terraform-clever-cloud#configurer-le-provider-clever-cloud" >}}), ainsi que comment [configurer un backend _via_ un bucket Cellar]({{< ref "/posts/2024-12-31-terraform-clever-cloud#configuration-du-backend" >}}).
Ces √©tapes ne sont pas d√©crites ici pour ne pas alourdir cet article, mais sont bien n√©cessaires.

## Cr√©ation de la base de donn√©es avec Terraform

La premi√®re √©tape consiste √† cr√©er une base de donn√©es consacr√©e √† Vault.
Avec Terraform, la cr√©ation de la base de donn√©es se fait avec le code suivant :

```terraform
resource "clevercloud_postgresql" "vault_storage" {
  name   = "vault_storage"
  plan   = "dev"
  region = "par"
}
```

Vault n√©cessite que le sch√©ma de la base de donn√©es soit initialis√© avant que l'application ne soit d√©marr√©.
Le sch√©ma est fourni dans la [documentation](https://developer.hashicorp.com/vault/docs/configuration/storage/postgresql) du backend :

```sql
CREATE TABLE vault_kv_store (
  parent_path TEXT COLLATE "C" NOT NULL,
  path        TEXT COLLATE "C",
  key         TEXT COLLATE "C",
  value       BYTEA,
  CONSTRAINT pkey PRIMARY KEY (path, key)
);

CREATE INDEX parent_path_idx ON vault_kv_store (parent_path);
```

Ce script peut √™tre pass√© √† la main via `psql`, ou dans la console Clever Cloud.

Il est aussi possible d'utiliser un `provisionner` Terraform pour ex√©cuter le script apr√®s la cr√©ation de la base de donn√©es :

```terraform
resource "clevercloud_postgresql" "vault_storage" {
  name = "vault_storage"
  plan = "dev"
  region = "par"

  provisioner "local-exec" {
    # wait for the database to be up
    command = "sleep 10 && psql -f vault-schema.sql"
    environment = {
      PGHOST = self.host
      PGPORT = self.port
      PGDATABASE = self.database
      PGUSER = self.user
      PGPASSWORD = self.password
    }
  }
}
```
Ici, le provisioner `local-exec` est utilis√© pour ex√©cuter la commande `psql` apr√®s avoir attendu quelques secondes, le temps que la base de donn√©es soit effectivement cr√©√©e.
Les variables d'environnement n√©cessaires √† l'ex√©cution de `psql` sont √©galement positionn√©es.

> Je ne suis pas un grand fan de l'ex√©cution de provisionners, car ils impliquent une d√©pendance avec la machine qui ex√©cute Terraform. Ici, c'est le binaire `psql` et la commande shell `sleep` qui sont n√©cessaires.

## Cr√©ation de l'instance Vault avec Terraform

Une fois la base de donn√©es cr√©√©e et le sch√©ma initialis√©, on peut cr√©er l'instance Docker pour notre Vault sur Clever Cloud avec le code suivant :

```terraform
resource "clevercloud_docker" "vault_instance" {
  name = "vault_instance"

  # vertical auto-scaling disabled
  smallest_flavor = "XS"
  biggest_flavor = "XS"

  # horizontal auto-scaling disabled
  min_instance_count = 1
  max_instance_count = 1

  # network setup
  additional_vhosts = ["vault-instance.cleverapps.io"]
  redirect_https = true

  # URL for the storage backend
  environment = {
    VAULT_LOCAL_CONFIG = jsonencode(
      {
        "storage" : {
          "postgresql" : {
            "connection_url" : "postgres://${clevercloud_postgresql.vault_storage.user}:${clevercloud_postgresql.vault_storage.password}@${clevercloud_postgresql.vault_storage.host}:${clevercloud_postgresql.vault_storage.port}/${clevercloud_postgresql.vault_storage.database}"
          }
        },
        "listener" : [{ "tcp" : { "address" : "0.0.0.0:8080", "tls_disable" : true } }],
        "disable_mlock" : true,
        "ui" : true
      })
  }
}
```

Parmi les param√®tres de configuration int√©ressants, on retrouve les param√®tres principaux de la ressource `¬¢levercloud_docker`, avec les param√®tres de scalabilit√© horizontale et verticale, ainsi que la d√©claration d'un nom de domaine customis√©.

Les variables d'environnement permettent de passer sa configuration √† Vault (plut√¥t que d'utiliser un fichier).
C'est un des aspects bien pratique de l'image Docker de Vault (document√© sur [dockerhub](https://hub.docker.com/r/hashicorp/vault)).
Ici, on utilise la variable `VAULT_LOCAL_CONFIG`, dans laquelle on donne du contenu format√© en _JSON_, √† l'aide de la fonction Terraform `jsonencode()`.

Concernant la configuration de Vault, le stockage sur l'instance PostgreSQL est d√©fini √† travers le param√®tre `"storage" : { "postgresql" : {} }`. L'URL de connexion est pass√© en param√®tre, elle est reconstruite √† partir des attributs de la ressource `clevercloud_postgresql.vault_storage`. Le param√®tre `listener` permet de forcer Vault √† √©couter sur le port `8080`, √† la place du port par d√©faut `8200` , qui est le port d'√©coute attendu par Clever Cloud. L'utilisation de l'adresse `0.0.0.0` permet aussi d'√©couter sur les connexions provenant d'internet (√† la place de l'adresse localhost `127.0.0.1` par d√©faut). C'est aussi Clever Cloud qui va s'occuper de l'exposition d'un certificat pour l'acc√®s en HTTPS √† l'instance, on d√©sactive donc le TLS avec l'option `tls_disable`. 
Enfin, on d√©sactive le lock de m√©moire en RAM avec `disable_mlock` car l'ex√©cution de containers Docker sur Clever Cloud ne permet pas √† ma connaissance l'utilisation de la capability Linux `IPC_LOCK`. Cette [capability](https://www.man7.org/linux/man-pages/man7/capabilities.7.html) de Linux permet de donner les droits √† un processus de locker sa m√©moire en RAM pour √©viter que la m√©moire soit √©crite sur le swap. Le param√®tre `ui` permet d'activer la console graphique de Vault, qui sera bien pratique pour les √©tapes suivantes.

Une fois l'application Docker cr√©√©e, on peut r√©cup√©rer son identifiant Clever Cloud avec un output Terraform :

```terraform
output "vault_instance_id" {
  description = "Clever Cloud id for the instance. Use with `clever link` before deploying."
  value = clevercloud_docker.vault_instance.id
}
```

```shell
$ terraform output -raw vault_instance_id        

app_72d4b5a4-1ab8-4653-a825-9be0c62e0fa1
```

Cet output permettra d'ex√©cuter les commande `clever link` et `clever deploy` pour d√©ployer l'instance Vault √† l'√©tape suivante.

## D√©ploiement de Vault

Le d√©ploiement d'une application Docker sur Clever Cloud passe par l'√©criture d'un `Dockerfile` et l'ex√©cution de la commande `clever deploy`

Le Dockerfile est simpliste :

```dockerfile
FROM hashicorp/vault:1.18

CMD ["server"]
```

On part d'une version fix√©e de Vault, la version 1.18 √©tant la plus r√©cente √† l'heure de l'√©criture de ces lignes, et on surcharge la commande ex√©cut√©e par Vault au d√©marrage de l'application avec la directive `CMD ["server"]`.
Par d√©fault, Vault d√©marre en mode "d√©veloppement", avec la commande `CMD ["server", "-dev"]`. Si vous souhaitez simplifier vos tests, vous pouvez conserver cette directive par d√©faut, mais elle est d√©conseill√©e pour de la production, donc je l'ai d√©sactiv√©e dans cet article.

Apr√®s avoir cr√©√© un repository Git pour notre fichier et commit√© celui-ci, le d√©ploiement se fait en 2 commandes, `clever link` pour associer le _repository_ Git courant √† l'instance Clever Cloud docker, puis `clever deploy` pour soumettre le code source √† Clever Cloud :

```shell
$ clever link app_72d4b5a4-1ab8-4653-a825-9be0c62e0fa1

Your application has been successfully linked!

$ clever deploy

Remote application is app_id=app_72d4b5a4-1ab8-4653-a825-9be0c62e0fa1, alias=vault_instance, name=vault_instance
Remote application belongs to orga_0331b635-5a61-4786-8f2f-dee81a1b8970
App is brand new, no commits on remote yet
New local commit to push is c6eb36c12ee5ca4a6f0cbcaa2683310856ef7f42 (from refs/heads/main)
Pushing source code to Clever Cloud
Your source code has been pushed to Clever Cloud.
Waiting for deployment to start
Deployment started (deployment_f5deb5ec-e9af-4f19-a5c2-978356632954)
Waiting for application logs
Couldn't start vault with IPC_LOCK. Disabling IPC_LOCK, please use --cap-add IPC_LOCK
==> Vault server configuration:
Administrative Namespace:
                     Cgo: disabled
   Environment Variables: APP_HOME, APP_ID, CC_APP_ID, CC_APP_NAME, CC_COMMIT_ID, CC_DEPLOYMENT_ID, CC_ENVIRON_UPDATE_TOKEN, CC_ENVIRON_UPDATE_URL, CC_INSTANCE_ID, CC_OWNER_ID, CC_PRETTY_INSTANCE_NAME, CC_REVERSE_PROXY_IPS, CC_USE_PULSAR_LOGSCOLLECTION, COMMIT_ID, HOME, HOSTNAME, INSTANCE_ID, INSTANCE_NUMBER, INSTANCE_TYPE, NAME, PATH, PORT, PWD, SHLVL, VAULT_LOCAL_CONFIG, VAULT_PG_CONNECTION_URL, VERSION
              Go Version: go1.23.3
              Listener 1: tcp (addr: "0.0.0.0:8080", cluster address: "0.0.0.0:8081", disable_request_limiter: "false", max_request_duration: "1m30s", max_request_size: "33554432", tls: "disabled")
               Log Level:
                   Mlock: supported: true, enabled: false
           Recovery Mode: false
                 Storage: postgresql (HA disabled)
                 Version: Vault v1.18.3, built 2024-12-16T14:00:53Z
             Version Sha: 7ae4eca5403bf574f142cd8f987b8d83bafcd1de
2025-01-03T14:25:52.301Z [INFO]  proxy environment: http_proxy="" https_proxy="" no_proxy=""
2025-01-03T14:25:52.333Z [INFO]  incrementing seal generation: generation=1
2025-01-03T14:25:52.333Z [WARN]  no `api_addr` value specified in config or in VAULT_API_ADDR; falling back to detection if possible, but this value should be manually set
2025-01-03T14:25:52.336Z [INFO]  core: Initializing version history cache for core
2025-01-03T14:25:52.336Z [INFO]  events: Starting event system
==> Vault server started! Log data will stream in below:
Application start successful
Successfully deployed in 0 minutes and 28 seconds
```

Au d√©marrage, Vault indique la configuration est bien charg√©e, et affiche quelques warnings.

> Concernant le warning mentionnant l'IPC_LOCK, il n'est pas possible √† ma connaissance de forcer l'option `--cap-add IPC_LOCK` sur Clever Cloud. N√©anmoins, ce warning ne pose pas de probl√®me, puisque le lock de la m√©moire est d√©sactiv√© avec le param√®tre `disable_mlock`.

Une fois le d√©marrage termin√©, la commande `clever open` permet d'ouvrir un navigateur web sur notre instance de Vault !

```shell
$ clever open

Opening the application in your browser
```

## Initialisation du Vault

Lors de sa premi√®re ouverture, Vault doit √™tre initialis√©, puis d√©verrouill√©, Ces √©tapes permettent de cr√©er ses cl√©s de d√©verrouillage (_unseal keys_), ainsi que le token d'acc√®s `root` qui permettra d'utiliser l'API dans un premier temps.

Ces op√©rations doivent √™tre faites une seule fois √† la cr√©ation du serveur Vault et doivent √™tre faites manuellement via le CLI Vault ou sa console.

![img.png](vault-create-root-keys.png)

Une fois le nombre de cl√©s choisies, ainsi que les diff√©rentes options de chiffrement, Vault g√©n√®re les cl√©s et les met √† disposition sur l'√©cran suivant :

![img.png](vault-root-keys.png)

> Ces cl√©s ne doivent √™tre perdues en aucune circonstance ! En cas d'utilisation en production, le nombre de cl√©s souhait√©es sera probablement diff√©rent de 1 !

Apr√®s avoir stock√© les cl√©s en lieu s√ªr, l'√©cran suivant nous invite √† d√©verrouiller Vault en saisissant une cl√© de d√©verrouillage.
Lorsque suffisamment de cl√©s auront √©t√© entr√©es, Vault sera d√©verrouill√© et pr√™t √† l'utilisation.

![img.png](vault-unseal.png)

Une fois Vault d√©verrouill√©, l'√©cran de login appara√Æt, il est alors possible de se logger avec le token d'acc√®s `root` obtenu aux √©tapes pr√©c√©dentes :

![img.png](vault-login.png)

La console de Vault est maintenant disponible :

![img.png](vault-console.png)

## Cr√©ation d'une policy puis un token pour Terraform

Maintenant que Vault est initialis√© et d√©verrouill√©, nous allons cr√©er une policy puis un token pour Terraform, afin d'√©viter d'utiliser le token `root` en permanence.

Apr√®s avoir positionn√© les variables d'environnement `VAULT_ADDR` et `VAULT_TOKEN` :

```shell
export VAULT_ADDR=https://vault-instance.cleverapps.io

# root token
export VAULT_TOKEN=hvs.EXRwc6DumFdDhLk4N8Xa
```

> Ce token est bien entendu un faux üè¥‚Äç‚ò†Ô∏è. Cela ne sert √† rien d'essayer de l'utiliser chez vous üòÜ.

Nous ex√©cutons alors le code Terraform suivant :

```terraform
resource "vault_policy" "terraform" {
  name = "terraform-policy"
  policy = file("terraform-policy.hcl")
}

resource "vault_token" "terraform_token" {
  display_name = "terraform-token"
  policies = ["terraform-policy"]

  ttl       = "24h"
  renewable = true
}

output "terraform_token" {
  sensitive = true
  value     = vault_token.terraform_token.client_token
}
```

La policy contient des droits d'administration pour Vault (requis pour Terraform). Elle a √©t√© copi√©e depuis le repository GitHub [hashicorp-education/learn-vault-codify](https://github.com/hashicorp-education/learn-vault-codify/blob/main/community/policies/admin-policy.hcl).

Elle contient en particulier une r√®gle qui permet la cr√©ation des mounts, ainsi qu'une r√®gle permettant la cr√©ation des secrets dans le mount qui sera consacr√© √† nos utilisateurs GitLab :

```hcl
path "sys/mounts/*"
{
  capabilities = ["create", "read", "update", "delete", "list", "sudo"]
}

path "gitlab_users_secrets/*"
{
  capabilities = ["create", "read", "update", "delete", "list", "sudo"]
}
```

Le token peut √™tre utilis√© pour le reste des op√©rations avec Terraform, on l'obtient avec la commande `terraform output` :

```shell
$ terraform output -raw terraform_token
 
hvs.rHfvBm3fVdXzedYefPQZwFtYotDMByf3iNh2qGnvnLACfpuhMT2GpE3hyzwAH2gGuV7EhQHbVDkZ9coPRG2Aa3aXFJv
```

> Encore un faux token pour l'exemple ici üè¥‚Äç‚ò†Ô∏è

## Initialisation de la configuration OIDC avec GitLab

Maintenant que Vault est d√©marr√© et pr√™t √† √™tre utilis√©, on peut maintenant configurer l'authentification OIDC avec GitLab.

La premi√®re √©tape consiste alors √† cr√©er dans GitLab une application OIDC repr√©sentant le Vault.

En fonction de votre instance GitLab et de vos droits d'acc√®s, la cr√©ation de cette application pourra √™tre faite directement dans la zone d'administration de GitLab, dans un groupe, ou sur votre profil personnel. Ces 3 options sont d√©crites dans la documentation de GitLab [Configure GitLab as an OAuth 2.0 authentication identity provider](https://docs.gitlab.com/ee/integration/oauth_provider.html).

Pour cet article, j'ai cr√©√© l'application dans mon profil personnel.

![](gitlab-vault-app.png)

Il est important de bien saisir l'URL de redirection qui sera utilis√©e par Vault. Ce format est pr√©cis√© dans la documentation de Vault [OIDC authentication](https://developer.hashicorp.com/vault/docs/auth/jwt#oidc-authentication). Cette URL prend la forme `https://{host:port}/ui/vault/auth/{path}/oidc/callback`.
Concernant les scopes, seul le scope `openid` est obligatoire.

Une fois ces informations saisies, on peut valider la cr√©ation de l'application :

![](gitlab-vault-id-secret.png)

Apr√®s validation, GitLab g√©n√®re un _Application ID_ et un _Secret_ qui permettront de configurer Vault.
Le secret peut √©galement √™tre re-g√©n√©r√© √† tout instant.

Une fois ces informations r√©cup√©r√©es, la configuration de GitLab en tant que provider OIDC pour Vault se fait avec les ressources Terraform `vault_jwt_auth_backend` et `vault_jwt_auth_backend_role`. La premi√®re ressource permet de configurer l'authentification avec Gitlab, la seconde permet de configurer comment le token √©mis par GitLab sera interpr√©t√© et quelles sont les _policies_ Vault √† assigner √† la personne authentifi√©e :

```terraform
resource "vault_jwt_auth_backend" "gitlab_oidc" {
  description = "GitLab OIDC authentication"
  path        = "gitlab"
  type        = "oidc"

  oidc_discovery_url = var.gitlab_url
  oidc_client_id     = var.gitlab_application_id
  oidc_client_secret = var.gitlab_secret
  bound_issuer       = var.gitlab_url

  tune {
    listing_visibility = "unauth"
  }

  default_role = "gitlab-user"
}

resource "vault_jwt_auth_backend_role" "gitlab_user" {
  backend         = vault_jwt_auth_backend.gitlab_oidc.path
  role_name       = "gitlab-user"
  token_policies  = ["read-secrets-policy"]

  user_claim            = "name"
  role_type             = "oidc"
  allowed_redirect_uris = ["${var.vault_url}/ui/vault/auth/${vault_jwt_auth_backend.gitlab_oidc.path}/oidc/callback"]
}
```

Les param√®tres `oidc` correspondent aux informations r√©cup√©r√©es √† l'√©tape pr√©c√©dente.
La valeur `gitlab` pour le param√®tre `path` permet de s'assurer que notre instance Vault utilisera bien cette valeur pour la g√©n√©ration de son URL de redirection, qui doit donc √™tre compatible avec celle d√©clar√©e dans GitLab.
Le param√®tre `tune.listing_visibility` permet de rendre visible le mode d'authentification aux utilisateurs non-authentifi√©s avec la valeur `unauth`. Enfin, on donne un r√¥le par d√©faut √† nos utilisateurs afin de pouvoir leur donner des acc√®s √† des secrets par la suite.

On d√©clare √©galement le r√¥le `gitlab-user`. Ce r√¥le permet de pr√©ciser quelle policy aura le token de l'utilisateur, ainsi que quel claim OIDC sera utilis√© pour identifier l'utilisateur dans Vault. Il faut aussi indiquer quelles sont les URLs de redirections autoris√©es par ce r√¥le.

La _policy_ associ√©e √† ce r√¥le permettra de lire des secrets dans un _mount_ consacr√© aux utilisateurs de GitLab :

```terraform
resource "vault_mount" "gitlab_secrets" {
  path        = "gitlab_users_secrets"
  type        = "kv"
  options = { version = "2" }
  description = "Secrets for the GitLab users"
}

resource "vault_policy" "read_secrets" {
  name = "read-secrets-policy"

  policy = <<EOT
# List, create, update, and delete key/value secrets at secret/
path "${vault_mount.gitlab_secrets.path}/*"
{
  capabilities = ["create", "read", "update", "delete", "list"]
}

EOT
}
```

Le mount est alors visible dans Vault :

![](vault-mount.png)

On peut alors cr√©er quelques secrets d'exemple avec des donn√©es al√©atoires :

```terraform
locals {
  secrets_count = 5
}

resource "random_pet" "username" {
  count = local.secrets_count
}

resource "random_password" "password" {
  count  = local.secrets_count
  length = 16
}

resource "vault_kv_secret_v2" "secret" {
  count = local.secrets_count

  data_json = jsonencode({
    username = random_pet.username[count.index].id
    password = random_password.password[count.index].result
  })
  mount = vault_mount.gitlab_secrets.path
  name  = random_pet.username[count.index].id
}
```

Une fois les secrets cr√©√©s, ils sont alors visibles directement dans Vault : 

![img.png](vault-secrets.png)

> Mon pr√©f√©r√© est le `supreme-caribou` ahah üòÜ

## Connexion avec GitLab

Tout est maintenant en place.

Lorsqu'on acc√®de √† la page de login de Vault, l'authentification _via_ GitLab est propos√©e.

![](vault-login-gitlab.png)

Lors de la premi√®re authentification d'un utilisateur, GitLab demandera √† l'utilisateur de confirmer qu'il souhaite bien utiliser son compte GitLab pour s'authentifier √† notre instance :

![](gitlab-vault-auth.png)

Une fois l'authentification termin√©e, l'utilisateur a acc√®s aux secrets :

![](vault-mount.png)

## En conclusion

Cet article a pr√©sent√© comment mettre en ≈ìuvre l'installation et la configuration d'un serveur Vault sur Clever Cloud, ainsi que la configuration de son authentification _via_ un provider OIDC, en l'occurrence GitLab.

Cette architecture n'est pas parfaite, mais permet de facilement d√©ployer un Vault pour des cas d'usage simples ou un environnement de dev.
Elle permet n√©anmoins de manipuler deux providers Terraform int√©ressants : le provider de Clever Cloud et le provider de Vault.

La configuration de l'application OIDC sur GitLab n'est malheureusement pas automatisable √† l'heure de l'√©criture de ces lignes, car aucune API ne permet de cr√©er les applications au niveau d'un groupe ou d'un utilisateur, mais uniquement au niveau de l'instance GitLab en elle-m√™me (cf. la documentation [Applications API](https://docs.gitlab.com/ee/api/applications.html)), ce qui restreint l'usage de cette API aux administrateurs de GitLab.
N√©anmoins, si votre instance GitLab est auto-g√©r√©e, il peut √™tre int√©ressant d'envisager l'utilisation de la ressource Terraform [`gitlab_application`](https://registry.terraform.io/providers/gitlabhq/gitlab/latest/docs/resources/application).


## Liens et r√©f√©rences

* Exemples de code de cet article sur [GitHub](https://github.com/juwit/terraform-clevercloud-playground/tree/main/vault)
* Page d'accueil de [Clever Cloud](https://www.clever-cloud.com/)
* Installation du [CLI Clever Cloud](https://www.clever-cloud.com/developers/doc/cli/getting_started/)
* Installation du [CLI Terraform](https://developer.hashicorp.com/terraform/install)
* Installation du [CLI OpenTofu](https://opentofu.org/docs/intro/install/)
* Documentation du provider [Terraform Clever Cloud](https://registry.terraform.io/providers/CleverCloud/clevercloud/latest):
  * Ressource [`clevercloud_postgresql`](https://registry.terraform.io/providers/CleverCloud/clevercloud/latest/docs/resources/postgresql)
  * Ressource [`clevercloud_docker`](https://registry.terraform.io/providers/CleverCloud/clevercloud/latest/docs/resources/docker)
* Documentation de [Vault](https://developer.hashicorp.com/vault/docs) :
  * L'image Docker de Vault sur [dockerhub](https://hub.docker.com/r/hashicorp/vault)
  * Configuration du [storage PostgreSQL](https://developer.hashicorp.com/vault/docs/configuration/storage/postgresql)
  * Policy exemple d'administration sur GitHub [hashicorp-education/learn-vault-codify](https://github.com/hashicorp-education/learn-vault-codify/blob/main/community/policies/admin-policy.hcl)
  * Configuration de l'[OIDC authentication](https://developer.hashicorp.com/vault/docs/auth/jwt#oidc-authentication)
* Documentation du provider [Terraform Vault](https://registry.terraform.io/providers/hashicorp/vault/latest/docs):
  * Ressource [`vault_mount`](https://registry.terraform.io/providers/hashicorp/vault/latest/docs/resources/mount)
  * Ressource [`vault_policy`](https://registry.terraform.io/providers/hashicorp/vault/latest/docs/resources/policy)
  * Ressource [`vault_token`](https://registry.terraform.io/providers/hashicorp/vault/latest/docs/resources/token)
  * Ressource [`vault_jwt_auth_backend`](https://registry.terraform.io/providers/hashicorp/vault/latest/docs/resources/jwt_auth_backend)
  * Ressource [`vault_kv_secret_v2`](https://registry.terraform.io/providers/hashicorp/vault/latest/docs/resources/kv_secret_v2)
* IPC_LOCK et mlock:
  * Manpage des [capabilities](https://www.man7.org/linux/man-pages/man7/capabilities.7.html) Linux (pour l'option `--cap-add IPC_LOCK`)
  * Manpage de l'appel syst√®me [mlock](https://www.man7.org/linux/man-pages/man2/mlock.2.html)
  * Un article [Vault and mlock()](https://support.hashicorp.com/hc/en-us/articles/115012787688-Vault-and-mlock) dans le help center de HashiCorp
  * Le param√®tre [`disable_mlock` dans la configuration de Vault](https://developer.hashicorp.com/vault/docs/configuration#disable_mlock)
* GitLab:
  * [Configure GitLab as an OAuth 2.0 authentication identity provider](https://docs.gitlab.com/ee/integration/oauth_provider.html)