---
date: "2025-02-20"
language: fr
tags:
- devops
- terraform
- vault
- gitlab
title: GitLab OIDC & Vault
series: Terraform et Clever Cloud
---

Cet article d√©crit comment utiliser Terraform et le _provider_ Vault pour configurer l'authentification OIDC avec GitLab. √Ä titre d'exemple, quelques secrets seront aussi stock√©s dans le Vault.

Le code de cet article est aussi disponible sur GitHub¬†: https://github.com/juwit/terraform-clevercloud-playground/tree/main/vault.

> Cet article a √©t√© √©crit avec des commandes Terraform, mais fonctionne √©galement avec les commandes OpenTofu √©quivalentes.

L'installation et la configuration initiale d'un Vault sur Clever Cloud est d√©crite dans l'article [Vault sur Clever Cloud](/posts/2025-01-03-vault-on-clever-cloud).

## Cr√©ation d'une policy puis un token pour Terraform

Lors de l'initialisation de Vault, on r√©cup√®re un _token_ `root`. Utiliser ce _token_ pour effectuer l'ensemble des op√©rations sur notre Vault n'est pas raisonnable. La premi√®re √©tape va alors consister √† utiliser ce _token_ `root`, pour en cr√©er un nouveau qui sera consacr√© √† Terraform. Ce nouveau _token_ disposera de droits √©lev√©s, mais pourra √™tre plus facilement invalid√© en cas de fuite de donn√©es. Et tout √ßa, nous le faisons en Terraform¬†!

Apr√®s avoir positionn√© les variables d'environnement `VAULT_ADDR` et `VAULT_TOKEN` (avec le _token_ `root` de Vault)¬†:

```shell
export VAULT_ADDR=https://vault-instance.cleverapps.io

# root token
export VAULT_TOKEN=hvs.EXRwc6DumFdDhLk4N8Xa
```

> Ce token est bien entendu un faux üè¥‚Äç‚ò†Ô∏èÔ∏è. Cela ne sert √† rien d'essayer de l'utiliser chez vous üòÜ

La configuration du _provider_ Vault est simple, puisqu'il se configure avec les variables d'environnement¬†:

```terraform
terraform {
  required_providers {
    vault = {
      source  = "hashicorp/vault"
      version = "4.5.0"
    }
  }
}

provider "vault" {
}
```

Nous ex√©cutons alors le code Terraform suivant¬†:

```terraform
resource "vault_policy" "terraform" {
  name = "terraform-policy"
  policy = file("terraform-policy.hcl")
}

resource "vault_token" "terraform_token" {
  display_name = "terraform-token"
  policies = ["terraform-policy"]

  ttl       = "24h"
  renewable = true
}

output "terraform_token" {
  sensitive = true
  value     = vault_token.terraform_token.client_token
}
```

La _policy_ contient des droits d'administration pour Vault (requis pour Terraform). Elle a √©t√© copi√©e depuis le _repository_ GitHub [hashicorp-education/learn-vault-codify](https://github.com/hashicorp-education/learn-vault-codify/blob/main/community/policies/admin-policy.hcl).

Elle contient en particulier une r√®gle qui permet la cr√©ation des _mounts_, ainsi qu'une r√®gle permettant la cr√©ation des secrets dans le _mount_ qui sera consacr√© √† nos utilisateurs GitLab¬†:

```hcl
path "sys/mounts/*"
{
  capabilities = ["create", "read", "update", "delete", "list", "sudo"]
}

path "gitlab_users_secrets/*"
{
  capabilities = ["create", "read", "update", "delete", "list", "sudo"]
}
```

Le _token_ peut √™tre utilis√© pour le reste des op√©rations avec Terraform, on l'obtient avec la commande `terraform output`¬†:

```shell
$ terraform output -raw terraform_token
 
hvs.rHfvBm3fVdXzedYefPQZwFtYotDMByf3iNh2qGnvnLACfpuhMT2GpE3hyzwAH2gGuV7EhQHbVDkZ9coPRG2Aa3aXFJv
```

> Encore un faux _token_ pour l'exemple ici üè¥‚Äç‚ò†Ô∏è

Le code de cette premi√®re partie est disponible dans le sous-r√©pertoire [03-terraform-token](https://github.com/juwit/terraform-clevercloud-playground/tree/main/vault/03-terraform-token) du _repository_ GitHub.

## Initialisation de la configuration OIDC avec GitLab

Pour cette deuxi√®me partie, le code Terraform que nous allons produire sera dans un nouveau _root module_.
Le code est disponible dans le sous-r√©pertoire [04-oidc-configuration](https://github.com/juwit/terraform-clevercloud-playground/tree/main/vault/04-oidc-configuration) du _repository_ GitHub. 

La configuration du _provider_ Vault est similaire en tout point √† celle de l'√©tape pr√©c√©dente¬†:

```terraform
terraform {
  required_providers {
    vault = {
      source  = "hashicorp/vault"
      version = "4.5.0"
    }
  }
}

provider "vault" {
}
```

Les variables d'environnement √† utiliser sont l'URL de Vault, ainsi que le token issu de l'√©tape pr√©c√©dente¬†: 

```shell
export VAULT_ADDR=https://vault-instance.cleverapps.io

# root token
export VAULT_TOKEN=hvs.rHfvBm3fVdXzedYefPQZwFtYotDMByf3iNh2qGnvnLACfpuhMT2GpE3hyzwAH2gGuV7EhQHbVDkZ9coPRG2Aa3aXFJv
```

Maintenant que Vault est configur√© pour √™tre utilis√© avec Terraform, on peut configurer l'authentification OIDC avec GitLab.

La premi√®re √©tape consiste alors √† cr√©er dans GitLab une application OIDC repr√©sentant le Vault.

En fonction de votre instance GitLab et de vos droits d'acc√®s, la cr√©ation de cette application pourra √™tre faite directement dans la zone d'administration de GitLab, dans un groupe, ou sur votre profil personnel. Ces 3 options sont d√©crites dans la documentation de GitLab [Configure GitLab as an OAuth 2.0 authentication identity provider](https://docs.gitlab.com/ee/integration/oauth_provider.html).

Pour cet article, j'ai cr√©√© l'application dans mon profil personnel.

![](gitlab-vault-app.png)

Il est important de bien saisir l'URL de redirection qui sera utilis√©e par Vault. Ce format est pr√©cis√© dans la documentation de Vault [OIDC authentication](https://developer.hashicorp.com/vault/docs/auth/jwt#oidc-authentication). Cette URL prend la forme `https://{host:port}/ui/vault/auth/{path}/oidc/callback`.
Concernant les _scopes_, seul `openid` est obligatoire pour l'utilisation de l'authentification.

Une fois ces informations saisies, on peut valider la cr√©ation de l'application¬†:

![](gitlab-vault-id-secret.png)

Apr√®s validation, GitLab g√©n√®re un _Application ID_ et un _Secret_ qui permettront de configurer Vault.
Le secret peut √©galement √™tre reg√©n√©r√© √† tout instant.

Une fois ces informations r√©cup√©r√©es, la configuration de GitLab en tant que _provider_ OIDC pour Vault se fait avec les ressources Terraform `vault_jwt_auth_backend` et `vault_jwt_auth_backend_role`. La premi√®re ressource permet de configurer l'authentification avec Gitlab, la seconde permet de configurer comment le _token_ √©mis par GitLab sera interpr√©t√© et quelles sont les _policies_ Vault √† assigner √† la personne authentifi√©e¬†:

```terraform
resource "vault_jwt_auth_backend" "gitlab_oidc" {
  description = "GitLab OIDC authentication"
  path        = "gitlab"
  type        = "oidc"

  oidc_discovery_url = var.gitlab_url
  oidc_client_id     = var.gitlab_application_id
  oidc_client_secret = var.gitlab_secret
  bound_issuer       = var.gitlab_url

  tune {
    listing_visibility = "unauth"
  }

  default_role = "gitlab-user"
}

resource "vault_jwt_auth_backend_role" "gitlab_user" {
  _backend_         = vault_jwt_auth_backend.gitlab_oidc.path
  role_name       = "gitlab-user"
  token_policies  = ["read-secrets-policy"]

  user_claim            = "name"
  role_type             = "oidc"
  allowed_redirect_uris = ["${var.vault_url}/ui/vault/auth/${vault_jwt_auth_backend.gitlab_oidc.path}/oidc/callback"]
}
```

Les param√®tres `oidc` correspondent aux informations r√©cup√©r√©es √† l'√©tape pr√©c√©dente.
La valeur `gitlab` pour le param√®tre `path` permet de s'assurer que notre instance Vault utilisera bien cette valeur pour la g√©n√©ration de son URL de redirection, qui doit donc √™tre compatible avec celle d√©clar√©e dans GitLab.
Le param√®tre `tune.listing_visibility` permet de rendre visible le mode d'authentification aux utilisateurs non authentifi√©s avec la valeur `unauth`. Enfin, on donne un r√¥le par d√©faut √† nos utilisateurs afin de pouvoir leur donner des acc√®s √† des secrets par la suite.

On d√©clare √©galement le r√¥le `gitlab-user`. Ce r√¥le permet de pr√©ciser quelle _policy_ aura le _token_ de l'utilisateur, ainsi que quel _claim_ OIDC sera utilis√© pour identifier l'utilisateur dans Vault. Il faut aussi indiquer quelles sont les URLs de redirections autoris√©es par ce r√¥le.

La _policy_ associ√©e √† ce r√¥le permettra de lire des secrets dans un _mount_ destin√© aux utilisateurs de GitLab¬†:

```terraform
resource "vault_mount" "gitlab_secrets" {
  path        = "gitlab_users_secrets"
  type        = "kv"
  options = { version = "2" }
  description = "Secrets for the GitLab users"
}

resource "vault_policy" "read_secrets" {
  name = "read-secrets-policy"

  policy = <<EOT
# List, create, update, and delete key/value secrets at secret/
path "${vault_mount.gitlab_secrets.path}/*"
{
  capabilities = ["create", "read", "update", "delete", "list"]
}

EOT
}
```

Le _mount_ est alors visible dans Vault¬†:

![](vault-mount.png)

On peut alors cr√©er quelques secrets d'exemple avec des donn√©es al√©atoires¬†:

```terraform
locals {
  secrets_count = 5
}

resource "random_pet" "username" {
  count = local.secrets_count
}

resource "random_password" "password" {
  count  = local.secrets_count
  length = 16
}

resource "vault_kv_secret_v2" "secret" {
  count = local.secrets_count

  data_json = jsonencode({
    username = random_pet.username[count.index].id
    password = random_password.password[count.index].result
  })
  mount = vault_mount.gitlab_secrets.path
  name  = random_pet.username[count.index].id
}
```

Une fois les secrets cr√©√©s, ils sont visibles directement dans Vault¬†:

![img.png](vault-secrets.png)

> Mon pr√©f√©r√© est le `supreme-caribou` ahah üòÜ

## Connexion avec GitLab

Tout est maintenant en place.

Lorsqu'on acc√®de √† la page de _login_ de Vault, l'authentification _via_ GitLab est propos√©e.

![](vault-login-gitlab.png)

Lors d'une premi√®re authentification, GitLab demandera √† l'utilisateur de confirmer qu'il souhaite bien utiliser son compte GitLab pour s'authentifier √† notre instance¬†:

![](gitlab-vault-auth.png)

Une fois l'authentification termin√©e, l'utilisateur a acc√®s aux secrets¬†:

![](vault-mount.png)

## En conclusion

Cet article a pr√©sent√© comment mettre en ≈ìuvre un serveur Vault pour configurer l'authentification _via_ un _provider_ OIDC, en l'occurrence GitLab.

Cette configuration n'est malheureusement pas automatisable enti√®rement √† l'heure de l'√©criture de ces lignes.
En effet, bien qu'il existe un _provider_ Terraform pour GitLab, aucune API ne permet de cr√©er les applications OIDC au niveau d'un groupe ou d'un utilisateur, mais uniquement au niveau de l'instance GitLab en elle-m√™me (cf. la documentation [Applications API](https://docs.gitlab.com/ee/api/applications.html)), ce qui restreint l'usage de cette API aux administrateurs de GitLab.
N√©anmoins, si votre instance GitLab est auto-g√©r√©e, il peut √™tre int√©ressant d'envisager l'utilisation de la ressource Terraform [`gitlab_application`](https://registry.terraform.io/providers/gitlabhq/gitlab/latest/docs/resources/application).

## Liens et r√©f√©rences

* Exemples de code de cet article sur [GitHub](https://github.com/juwit/terraform-clevercloud-playground/tree/main/vault)
  * [G√©n√©ration de la policy et du token Terraform](https://github.com/juwit/terraform-clevercloud-playground/tree/main/vault/03-terraform-token)
  * [Configuration Gitlab OIDC](https://github.com/juwit/terraform-clevercloud-playground/tree/main/vault/04-oidc-configuration)
* Installation du [CLI Terraform](https://developer.hashicorp.com/terraform/install)
* Installation du [CLI OpenTofu](https://opentofu.org/docs/intro/install/)
* Documentation de [Vault](https://developer.hashicorp.com/vault/docs)¬†:
  * L'image Docker de Vault sur [dockerhub](https://hub.docker.com/r/hashicorp/vault)
  * Configuration du [storage PostgreSQL](https://developer.hashicorp.com/vault/docs/configuration/storage/postgresql)
  * _Policy_ exemple d'administration sur GitHub [hashicorp-education/learn-vault-codify](https://github.com/hashicorp-education/learn-vault-codify/blob/main/community/policies/admin-policy.hcl)
  * Configuration de l'[OIDC authentication](https://developer.hashicorp.com/vault/docs/auth/jwt#oidc-authentication)
* Documentation du _provider_ [Terraform Vault](https://registry.terraform.io/providers/hashicorp/vault/latest/docs)¬†:
  * Ressource [`vault_mount`](https://registry.terraform.io/providers/hashicorp/vault/latest/docs/resources/mount)
  * Ressource [`vault_policy`](https://registry.terraform.io/providers/hashicorp/vault/latest/docs/resources/policy)
  * Ressource [`vault_token`](https://registry.terraform.io/providers/hashicorp/vault/latest/docs/resources/token)
  * Ressource [`vault_jwt_auth_backend`](https://registry.terraform.io/providers/hashicorp/vault/latest/docs/resources/jwt_auth_backend)
  * Ressource [`vault_kv_secret_v2`](https://registry.terraform.io/providers/hashicorp/vault/latest/docs/resources/kv_secret_v2)
* GitLab¬†:
  * [Configure GitLab as an OAuth 2.0 authentication identity provider](https://docs.gitlab.com/ee/integration/oauth_provider.html)
* Photo de couverture par [Ray Hennessy](https://unsplash.com/@rayhennessy?utm_content=creditCopyText&utm_medium=referral&utm_source=unsplash) sur [Unsplash](https://unsplash.com/photos/brown-fox-on-snow-field-xUUZcpQlqpM?utm_content=creditCopyText&utm_medium=referral&utm_source=unsplash)