---
date: "2025-01-17"
lastmod: "2025-02-12"
language: fr
tags:
- devops
- terraform
- vault
- clevercloud
title: Vault sur Clever Cloud
series: Terraform et Clever Cloud
---

Pour les besoins des cours que je donne √† l'Universit√© de Lille, j'ai d√ª configurer un serveur Vault sur Clever Cloud.

Et [bien entendu](/books/iac-avec-terraform), j'ai fait tout √ßa avec Terraform.

Cet article d√©crit comment utiliser le provider Terraform de Clever Cloud pour d√©ployer un serveur Vault. Un article suivant d√©crira comment le configurer pour l'authentification OIDC avec GitLab et y stocker quelques secrets √† titre d'exemple.

Le code de cet article est aussi disponible sur GitHub¬†: https://github.com/juwit/terraform-clevercloud-playground/tree/main/vault.

> Cet article a √©t√© √©crit avec des commandes Terraform, mais fonctionne √©galement avec les commandes OpenTofu √©quivalentes.

## Architecture cible

Avant d'entrer dans la mise en pratique, il convient ici d'expliquer quelques choix illustr√©s par le sch√©ma suivant.

![](cc_vault.png)

Clever Cloud propose de d√©ployer des applications dans de [nombreux langages](https://www.clever-cloud.com/developers/doc/applications/). Pour h√©berger une instance Vault, le plus simple semblait d'utiliser une instance Docker.

Par d√©fault, Vault propose l'utilisation du _backend_ de stockage _Integrated storage_ pour le stockage des donn√©es. √âtant donn√© la nature du d√©ploiement avec une instance Docker sur un seul n≈ìud et le fait que Clever Cloud ne supporte pas le stockage persistant pour ce type d'instance, il m'a sembl√© judicieux d'utiliser un _backend_ de stockage externalis√©. Parmi les options propos√©es par Vault, 3 options sont envisageables sur Clever Cloud¬†: les bases de donn√©es MySQL ou PostgreSQL, ou S3 _via_ l'impl√©mentation _Cellar_ fournie par Clever Cloud.

Le stockage externalis√© sur S3 ne supporte pas la haute disponibilit√© et pourrait s'av√©rer incompatible avec l'impl√©mentation _Cellar_ (cf. [les adaptations requises par le _backend_ Terraform S3 pour _Cellar_](/posts/2024-12-31-terraform-clever-cloud#configuration-du-backend)), donc je l'ai directement √©cart√© et j'ai privil√©gi√© l'impl√©mentation avec PostgreSQL.

L'authentification _via_ GitLab permet √† mes √©tudiants d'utiliser leur compte GitLab existant, en exploitant l'instance GitLab fournie par l'Universit√© de Lille. C'est donc tr√®s pratique pour eux (pas besoin d'avoir un compte ailleurs) et pour moi (pas besoin de cr√©er et de fournir des comptes).
J'aurais aussi pu utiliser une instance KeyCloak pour impl√©menter l'authentification, mais cela aurait complexifi√© inutilement l'impl√©mentation.

√Ä noter aussi que je ne suis pas expert Vault, donc je ne suis pas √† l'abri d'avoir fait une erreur de configuration quelque part, alors attention si vous utilisez cette configuration en production üôÇ

## SetUp de Terraform

Dans un article pr√©c√©dent, j'ai d√©j√† expliqu√© comment configurer [Terraform pour Clever Cloud](/posts/2024-12-31-terraform-clever-cloud#configurer-le-provider-clever-cloud), ainsi que comment [configurer un _backend_ _via_ un _bucket_ _Cellar_](/posts/2024-12-31-terraform-clever-cloud#configuration-du-backend).
Ces √©tapes ne sont pas d√©crites ici pour ne pas alourdir cet article, mais sont bien n√©cessaires.

## Cr√©ation de la base de donn√©es avec Terraform

La premi√®re √©tape consiste √† cr√©er une base de donn√©es consacr√©e √† Vault.
Avec Terraform, la cr√©ation de la base de donn√©es se fait avec le code suivant¬†:

```terraform
resource "clevercloud_postgresql" "vault_storage" {
  name   = "vault_storage"
  plan   = "dev"
  region = "par"
}
```

Vault n√©cessite que le sch√©ma de la base de donn√©es soit initialis√© avant que l'application ne soit d√©marr√©e.
Le sch√©ma est fourni dans la [documentation](https://developer.hashicorp.com/vault/docs/configuration/storage/postgresql) du _backend_¬†:

```sql
CREATE TABLE vault_kv_store (
  parent_path TEXT COLLATE "C" NOT NULL,
  path        TEXT COLLATE "C",
  key         TEXT COLLATE "C",
  value       BYTEA,
  CONSTRAINT pkey PRIMARY KEY (path, key)
);

CREATE INDEX parent_path_idx ON vault_kv_store (parent_path);
```

Ce script peut √™tre pass√© √† la main _via_ `psql`, ou dans la console Clever Cloud.

Il est aussi possible d'utiliser un _provisioner_ Terraform pour ex√©cuter le script apr√®s la cr√©ation de la base de donn√©es¬†:

```terraform
resource "clevercloud_postgresql" "vault_storage" {
  name = "vault_storage"
  plan = "dev"
  region = "par"

  provisioner "local-exec" {
    # wait for the database to be up
    command = "sleep 10 && psql -f vault-schema.sql"
    environment = {
      PGHOST = self.host
      PGPORT = self.port
      PGDATABASE = self.database
      PGUSER = self.user
      PGPASSWORD = self.password
    }
  }
}
```
Ici, le _provisioner_ `local-exec` est utilis√© pour ex√©cuter la commande `psql` apr√®s avoir attendu quelques secondes, le temps que la base de donn√©es soit effectivement cr√©√©e.
Les variables d'environnement n√©cessaires √† l'ex√©cution de `psql` sont √©galement positionn√©es.

> Je ne suis pas un grand fan de l'ex√©cution de _provisioners_, car ils impliquent une d√©pendance avec la machine qui ex√©cute Terraform. Ici, c'est le binaire `psql` et la commande `sleep` dans le script _shell_ qui sont n√©cessaires.

## Cr√©ation de l'instance Vault avec Terraform

Une fois la base de donn√©es cr√©√©e et le sch√©ma initialis√©, on peut cr√©er l'instance Docker pour notre Vault sur Clever Cloud avec le code suivant¬†:

```terraform
resource "clevercloud_docker" "vault_instance" {
  name = "vault_instance"

  # vertical auto-scaling disabled
  smallest_flavor = "XS"
  biggest_flavor = "XS"

  # horizontal auto-scaling disabled
  min_instance_count = 1
  max_instance_count = 1

  # network setup
  additional_vhosts = ["vault-instance.cleverapps.io"]
  redirect_https = true

  # URL for the storage backend
  environment = {
    VAULT_LOCAL_CONFIG = jsonencode(
      {
        "storage": {
          "postgresql": {
            "connection_url": "postgres://${clevercloud_postgresql.vault_storage.user}:${clevercloud_postgresql.vault_storage.password}@${clevercloud_postgresql.vault_storage.host}:${clevercloud_postgresql.vault_storage.port}/${clevercloud_postgresql.vault_storage.database}"
          }
        },
        "listener": [{ "tcp": { "address": "0.0.0.0:8080", "tls_disable": true } }],
        "disable_mlock": true,
        "ui": true
      })
  }
}
```

Parmi les param√®tres de configuration int√©ressants, on retrouve les param√®tres principaux de la ressource `clevercloud_docker`, avec les param√®tres de scalabilit√© horizontale et verticale, ainsi que la d√©claration d'un nom de domaine customis√©.

Les variables d'environnement permettent de passer sa configuration √† Vault (plut√¥t que d'utiliser un fichier).
C'est un des aspects bien pratique de l'image Docker de Vault (document√© sur [dockerhub](https://hub.docker.com/r/hashicorp/vault)).
Ici, on utilise la variable `VAULT_LOCAL_CONFIG`, dans laquelle on donne du contenu format√© en _JSON_, √† l'aide de la fonction Terraform `jsonencode()`.

Concernant la configuration de Vault, le stockage sur l'instance PostgreSQL est d√©fini √† travers le param√®tre `"storage"¬†: { "postgresql"¬†: {} }`. L'URL de connexion est pass√©e en param√®tre, elle est reconstruite √† partir des attributs de la ressource `clevercloud_postgresql.vault_storage`. Le param√®tre `listener` permet de forcer Vault √† √©couter sur le port `8080`, √† la place du port par d√©faut `8200`, qui est le port d'√©coute attendu par Clever Cloud. L'utilisation de l'adresse `0.0.0.0` permet aussi d'√©couter sur les connexions provenant d'internet (√† la place de l'adresse localhost `127.0.0.1` par d√©faut). C'est aussi Clever Cloud qui va s'occuper de l'exposition d'un certificat pour l'acc√®s en HTTPS √† l'instance, on d√©sactive donc le TLS avec l'option `tls_disable`.
Enfin, on d√©sactive le _lock_ de m√©moire en RAM avec `disable_mlock`, car l'ex√©cution de _containers_ Docker sur Clever Cloud ne permet pas, √† ma connaissance, l'utilisation de la _capability_ Linux `IPC_LOCK`. Cette [capability](https://www.man7.org/linux/man-pages/man7/capabilities.7.html) de Linux permet de donner les droits √† un processus de verrouiller sa m√©moire en RAM pour √©viter que la m√©moire soit √©crite sur le _swap_. Le param√®tre `ui` permet d'activer la console graphique de Vault, qui sera bien pratique pour les √©tapes suivantes.

Une fois l'application Docker cr√©√©e, on peut r√©cup√©rer son identifiant Clever Cloud avec un _output_ Terraform¬†:

```terraform
output "vault_instance_id" {
  description = "Clever Cloud id for the instance. Use with `clever link` before deploying."
  value = clevercloud_docker.vault_instance.id
}
```

```shell
$ terraform output -raw vault_instance_id        

app_72d4b5a4-1ab8-4653-a825-9be0c62e0fa1
```

Cet _output_ permettra d'ex√©cuter les commande `clever link` et `clever deploy` pour d√©ployer l'instance Vault √† l'√©tape suivante.

## D√©ploiement de Vault

Le d√©ploiement d'une application Docker sur Clever Cloud passe par l'√©criture d'un `Dockerfile` et l'ex√©cution de la commande `clever deploy`.

Le contenu du fichier `Dockerfile` est simpliste¬†:

```dockerfile
FROM hashicorp/vault:1.18

CMD ["server"]
```

On part d'une version fix√©e de Vault, (la version 1.18 √©tant la plus r√©cente √† l'heure de l'√©criture de ces lignes), et on surcharge la commande ex√©cut√©e par Vault au d√©marrage de l'application avec la directive `CMD ["server"]`.
Par d√©faut, Vault d√©marre en mode ¬´¬†d√©veloppement¬†¬ª, avec la commande `CMD ["server", "-dev"]`. Si vous souhaitez simplifier vos tests, vous pouvez conserver cette directive, mais elle est d√©conseill√©e pour de la production. Je l'ai donc d√©sactiv√©e dans cet article.

Apr√®s avoir cr√©√© un _repository_ Git pour notre fichier et commit√© celui-ci, le d√©ploiement se fait en 2 commandes, `clever link` pour associer le _repository_ Git courant √† l'instance Clever Cloud Docker, puis `clever deploy` pour soumettre le code source √† Clever Cloud¬†:

```shell
$ clever link app_72d4b5a4-1ab8-4653-a825-9be0c62e0fa1

Your application has been successfully linked!

$ clever deploy

Remote application is app_id=app_72d4b5a4-1ab8-4653-a825-9be0c62e0fa1, alias=vault_instance, name=vault_instance
Remote application belongs to orga_0331b635-5a61-4786-8f2f-dee81a1b8970
App is brand new, no commits on remote yet
New local commit to push is c6eb36c12ee5ca4a6f0cbcaa2683310856ef7f42 (from refs/heads/main)
Pushing source code to Clever Cloud
Your source code has been pushed to Clever Cloud.
Waiting for deployment to start
Deployment started (deployment_f5deb5ec-e9af-4f19-a5c2-978356632954)
Waiting for application logs
Couldn't start vault with IPC_LOCK. Disabling IPC_LOCK, please use --cap-add IPC_LOCK
==> Vault server configuration:
Administrative Namespace:
                     Cgo: disabled
   Environment Variables: APP_HOME, APP_ID, CC_APP_ID, CC_APP_NAME, CC_COMMIT_ID, CC_DEPLOYMENT_ID, CC_ENVIRON_UPDATE_TOKEN, CC_ENVIRON_UPDATE_URL, CC_INSTANCE_ID, CC_OWNER_ID, CC_PRETTY_INSTANCE_NAME, CC_REVERSE_PROXY_IPS, CC_USE_PULSAR_LOGSCOLLECTION, COMMIT_ID, HOME, HOSTNAME, INSTANCE_ID, INSTANCE_NUMBER, INSTANCE_TYPE, NAME, PATH, PORT, PWD, SHLVL, VAULT_LOCAL_CONFIG, VAULT_PG_CONNECTION_URL, VERSION
              Go Version: go1.23.3
              Listener 1: tcp (addr: "0.0.0.0:8080", cluster address: "0.0.0.0:8081", disable_request_limiter: "false", max_request_duration: "1m30s", max_request_size: "33554432", tls: "disabled")
               Log Level:
                   Mlock: supported: true, enabled: false
           Recovery Mode: false
                 Storage: postgresql (HA disabled)
                 Version: Vault v1.18.3, built 2024-12-16T14:00:53Z
             Version Sha: 7ae4eca5403bf574f142cd8f987b8d83bafcd1de
2025-01-03T14:25:52.301Z [INFO]  proxy environment: http_proxy="" https_proxy="" no_proxy=""
2025-01-03T14:25:52.333Z [INFO]  incrementing seal generation: generation=1
2025-01-03T14:25:52.333Z [WARN]  no `api_addr` value specified in config or in VAULT_API_ADDR; falling back to detection if possible, but this value should be manually set
2025-01-03T14:25:52.336Z [INFO]  core: Initializing version history cache for core
2025-01-03T14:25:52.336Z [INFO]  events: Starting event system
==> Vault server started! Log data will stream in below:
Application start successful
Successfully deployed in 0 minutes and 28 seconds
```

Au d√©marrage, Vault indique que la configuration est bien charg√©e, et affiche quelques _warnings_.

> Concernant le _warning_ mentionnant l'IPC_LOCK, il n'est pas possible √† ma connaissance de forcer l'option `--cap-add IPC_LOCK` sur Clever Cloud. N√©anmoins, ce _warning_ ne pose pas de probl√®me, puisque le _lock_ de la m√©moire est d√©sactiv√© avec le param√®tre `disable_mlock`.

Une fois le d√©marrage termin√©, la commande `clever open` permet d'ouvrir un navigateur web sur notre instance de Vault¬†!

```shell
$ clever open

Opening the application in your browser
```

L'URL d'acc√®s √† Vault peut aussi √™tre r√©cup√©r√©e de deux mani√®res¬†: avec la commande `clever domain`, ou avec un _output_ Terraform qu'on ajoute au code qui cr√©e l'instance Docker.

```shell
$ clever domain
  app_72d4b5a4-1ab8-4653-a825-9be0c62e0fa1.cleverapps.io
* vault-instance.cleverapps.io
```

```terraform
output "vault_url" {
  description = "URL of the Vault instance."
  value = clevercloud_docker.vault_instance.vhost
}
```

```shell
$ terraform output -raw vault_url

app_72d4b5a4-1ab8-4653-a825-9be0c62e0fa1.cleverapps.io
```

## Initialisation du Vault

Lors de sa premi√®re ouverture, Vault doit √™tre initialis√©, puis d√©verrouill√©. Ces √©tapes permettent de cr√©er ses cl√©s de d√©verrouillage (_unseal keys_), ainsi que le _token_ d'acc√®s `root` qui permettra d'utiliser l'API dans un premier temps.

Ces op√©rations doivent √™tre faites une seule fois √† la cr√©ation du serveur Vault et doivent √™tre faites manuellement _via_ le CLI Vault ou sa console. Dans cet exemple, nous allons effectuer ces manipulations dans la console de Vault¬†:

![img.png](vault-create-root-keys.png)

Une fois le nombre de cl√©s choisi, ainsi que les diff√©rentes options de chiffrement, Vault g√©n√®re les cl√©s et les met √† disposition sur l'√©cran suivant¬†:

![img.png](vault-root-keys.png)

> Ces cl√©s ne doivent √™tre perdues en aucune circonstance¬†! En cas d'utilisation en production, le nombre de cl√©s souhait√© sera probablement diff√©rent de 1¬†!

Apr√®s avoir stock√© les cl√©s en lieu s√ªr, l'√©cran suivant nous invite √† d√©verrouiller Vault en saisissant une cl√© de d√©verrouillage.
Lorsque suffisamment de cl√©s auront √©t√© entr√©es, Vault sera d√©verrouill√© et pr√™t √† l'utilisation.

![img.png](vault-unseal.png)

Une fois Vault d√©verrouill√©, l'√©cran de _login_ appara√Æt, il est alors possible de se connecter avec le _token_ d'acc√®s `root` obtenu aux √©tapes pr√©c√©dentes¬†:

![img.png](vault-login.png)

La console de Vault est maintenant disponible¬†:

![img.png](vault-console.png)

Vault est maintenant initialis√©, d√©verrouill√© et pr√™t √† √™tre utilis√©¬†!

L'article suivant traitera de la configuration de Vault pour utiliser l'authentification OIDC de GitLab, et finaliser cette architecture.

## En conclusion

Cet article a pr√©sent√© comment mettre en ≈ìuvre l'installation et la configuration d'un serveur Vault sur Clever Cloud.

C'est cette infrastructure qui m'a permis de pouvoir mettre √† disposition rapidement un serveur Vault pour mes √©tudiants, afin de les former √† la r√©cup√©ration de secrets depuis une application Spring Boot.

Pour ex√©cuter l'infrastructure propos√©e dans cet article, il vous en co√ªtera environ 16¬†‚Ç¨/mois avec les plans utilis√©s¬†:

| article          | prix/mois |
|------------------|-----------|
| PostgreSQL - Dev | 0&nbsp;‚Ç¨  |
| Docker - Plan XS | 16&nbsp;‚Ç¨ |

Cette architecture n'est pas parfaite, mais permet de facilement d√©ployer un Vault pour des cas d'usage simples ou un environnement de dev. Il faudrait bien entendu la revoir (en particulier les plans utilis√©s) pour un environnement de production.

## Liens et r√©f√©rences

* Exemples de code de cet article sur [GitHub](https://github.com/juwit/terraform-clevercloud-playground/tree/main/vault)
* Page d'accueil de [Clever Cloud](https://www.clever-cloud.com/)
* Installation du [CLI Clever Cloud](https://www.clever-cloud.com/developers/doc/cli/getting_started/)
* Installation du [CLI Terraform](https://developer.hashicorp.com/terraform/install)
* Installation du [CLI OpenTofu](https://opentofu.org/docs/intro/install/)
* Documentation du provider [Terraform Clever Cloud](https://registry.terraform.io/providers/CleverCloud/clevercloud/latest)¬†:
  * Ressource [`clevercloud_postgresql`](https://registry.terraform.io/providers/CleverCloud/clevercloud/latest/docs/resources/postgresql)
  * Ressource [`clevercloud_docker`](https://registry.terraform.io/providers/CleverCloud/clevercloud/latest/docs/resources/docker)
* Documentation de [Vault](https://developer.hashicorp.com/vault/docs)¬†:
  * L'image Docker de Vault sur [dockerhub](https://hub.docker.com/r/hashicorp/vault)
  * Configuration du [storage PostgreSQL](https://developer.hashicorp.com/vault/docs/configuration/storage/postgresql)
* IPC_LOCK et mlock¬†:
  * Manpage des [capabilities](https://www.man7.org/linux/man-pages/man7/capabilities.7.html) Linux (pour l'option `--cap-add IPC_LOCK`)
  * Manpage de l'appel syst√®me [mlock](https://www.man7.org/linux/man-pages/man2/mlock.2.html)
  * Un article [Vault and mlock()](https://support.hashicorp.com/hc/en-us/articles/115012787688-Vault-and-mlock) dans le help center de HashiCorp
  * Le param√®tre [`disable_mlock` dans la configuration de Vault](https://developer.hashicorp.com/vault/docs/configuration#disable_mlock)
* Photo de couverture par [Jason Dent](https://unsplash.com/@jdent?utm_content=creditCopyText&utm_medium=referral&utm_source=unsplash) sur [Unsplash](https://unsplash.com/photos/black-and-silver-door-knob-3wPJxh-piRw?utm_content=creditCopyText&utm_medium=referral&utm_source=unsplash)
